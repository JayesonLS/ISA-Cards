/* Implements glue logic. */

Name     Tandy1000Essentials ;
PartNo   T1KTURBO ;
Date     06/06/2021 ;
Revision 01 ;
Designer Jayeson Lee-Steere ;
Company  Titanium Studios Pty Ltd ;
Assembly None ;
Location  ;
Device   G22V10;

/*
 * 22V10 GAL pinout, DIP, top view
 *
 * I/CLK.[ 1     24 ].VCC
 *     I.[ 2     23 ].I/O/Q
 *     I.[ 3     22 ].I/O/Q
 *     I.[ 4     21 ].I/O/Q
 *     I.[ 5     20 ].I/O/Q
 *     I.[ 6     19 ].I/O/Q
 *     I.[ 7     18 ].I/O/Q
 *     I.[ 8     17 ].I/O/Q
 *     I.[ 9     16 ].I/O/Q
 *     I.[ 10    15 ].I/O/Q
 *     I.[ 11    14 ].I/O/Q
 *   GND.[ 12    13 ].I
 */

/* *************** INPUT PINS *********************/
PIN  1  = A04                    ; /* ISA address line */
PIN  2  = A05                    ; /* ISA address line */
PIN  3  = A06                    ; /* ISA address line */
PIN  4  = A07                    ; /* ISA address line */
PIN  5  = A08                    ; /* ISA address line */
PIN  6  = A09                    ; /* ISA address line */
PIN  7  = RESET_IN               ; /* ISA reset */
PIN  8  = A15                    ; /* ISA address line */
PIN  9  = A16                    ; /* ISA address line */
PIN 10  = A17                    ; /* ISA address line */
PIN 11  = A18                    ; /* ISA address line */
PIN 13  = A19                    ; /* ISA address line */
PIN 17  = !MEMR                  ; /* ISA memory read */
PIN 21  = !IOR                   ; /* ISA IO Read */
PIN 22  = ATA_ADDR_JMPR          ; /* ATA Address Jumper */
PIN 23  = ROM_ADDR_JMPR          ; /* BIOS ROM Address Jumper */

/* *************** OUTPUT PINS *********************/
PIN 14  = !IOR_DELAYED           ; /* IOR, delayed to come slightly after setting BUF_DIR for reads. */
PIN 16  = !MEMR_DELAYED          ; /* MEMR, delayed to come slightly after setting BUF_DIR for reads. */
PIN 15  = !BUF_DIR               ; /* Whether reading from board. Selects direction of bus transceiver. */
PIN 18  = !CE_BIOS_DECODE        ; /* Chip select BIOS Flash ROM. */
PIN 19  = !CS1_ATA               ; /* Chip select 1 for ATA. */
PIN 20  = !RESET_OUT             ; /* Inverted reset */

/* Equations */
DECODE_300 = A09 & A08 & !A07 & !A06 & !A05 & !A04;
DECODE_320 = A09 & A08 & !A07 & !A06 &  A05 & !A04;
CS1_ATA_TEMP = (DECODE_300 & !ATA_ADDR_JMPR) # (DECODE_320 & ATA_ADDR_JMPR);

DECODE_D000 = A19 & A18 & !A17 &  A16 & !A15;
DECODE_C800 = A19 & A18 & !A17 & !A16 &  A15;
CE_BIOS_DECODE_TEMP = (DECODE_D000 & !ROM_ADDR_JMPR) # (DECODE_C800 & ROM_ADDR_JMPR);

/* RAM_DECODE = !A19; *//* Lower 512K */
RAM_DECODE = 'b'0; 
BUF_DIR_TEMP = (IOR & CS1_ATA_TEMP) # (MEMR & (CE_BIOS_DECODE_TEMP # RAM_DECODE));

/* Outputs */
RESET_OUT = RESET_IN; /* Only for inverting reset signal. */
CS1_ATA = CS1_ATA_TEMP;
CE_BIOS_DECODE = CE_BIOS_DECODE_TEMP;
BUF_DIR = BUF_DIR_TEMP;
/* Intentionally uses BUF_DIR rather than BUF_DIR_TEMP to slightly delay the 
   IOR and MEMR signals. This is to ensure the buffer direction does not change
   **after** the devices on the board are trying to output values. */
IOR_DELAYED = BUF_DIR & IOR;
MEMR_DELAYED = BUF_DIR & MEMR;
