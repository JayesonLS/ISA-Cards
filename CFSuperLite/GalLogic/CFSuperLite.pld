/* Implements glue logic. */
Name     CfSuperLite;
PartNo   CFSL ;
Date     09/10/2021 ;
Revision 01 ;
Designer Jayeson Lee-Steere ;
Company  Titanium Studios Pty Ltd ;
Assembly None ;
Location  ;
Device   G16V8AS;

/* *************** INPUT PINS *********************/
PIN  1  = A16                    ; /* ISA address line */
PIN  2  = A18                    ; /* ISA address line */
PIN  3  = A15                    ; /* ISA address line */
PIN  4  = A06                    ; /* ISA address line */
PIN  5  = A07                    ; /* ISA address line */
PIN  6  = A08                    ; /* ISA address line */
PIN  7  = A09                    ; /* ISA address line */
PIN  8  = A04                    ; /* ISA address line */
PIN  9  = RESET_IN               ; /* ISA reset */
PIN 11  = A19                    ; /* ISA address line */
PIN 12  = A17                    ; /* ISA address line */
PIN 13  = A05                    ; /* ISA address line */
PIN 17  = AEN                    ; /* ISA memory read */
PIN 18  = ATA_ADDR_JMPR          ; /* ATA Address Jumper */
PIN 19  = ROM_ADDR_JMPR          ; /* BIOS ROM Address Jumper */

/* *************** OUTPUT PINS *********************/
PIN 14  = !CE_BIOS_DECODE        ; /* Chip select BIOS Flash ROM. */
PIN 15  = !CS1_ATA               ; /* Chip select 1 for ATA. */
PIN 16  = !RESET_OUT             ; /* Inverted reset */

/* Equations */
DECODE_300 = A09 & A08 & !A07 & !A06 & !A05 & !A04;
DECODE_320 = A09 & A08 & !A07 & !A06 &  A05 & !A04;
CS1_ATA_TEMP = (DECODE_300 & !ATA_ADDR_JMPR) # (DECODE_320 & ATA_ADDR_JMPR);

DECODE_D000 = A19 & A18 & !A17 &  A16 & !A15;
DECODE_C800 = A19 & A18 & !A17 & !A16 &  A15;
CE_BIOS_DECODE_TEMP = (DECODE_D000 & !ROM_ADDR_JMPR) # (DECODE_C800 & ROM_ADDR_JMPR);

/* Outputs */
RESET_OUT = RESET_IN; /* Only for inverting reset signal. */
CS1_ATA = !AEN & CS1_ATA_TEMP;
CE_BIOS_DECODE = CE_BIOS_DECODE_TEMP;

